
&НаКлиенте
Процедура УстановитьВидимость()
	
	Если НЕ ЗначениеЗаполнено(Объект.ИмяФайла) Тогда
		Элементы.ПрикрепленныйФайл.Заголовок = "Прикрепить файл...";
		Элементы.СохранитьФайл.Видимость = Ложь;
		Элементы.УдалитьФайл.Видимость = Ложь;
	Иначе
		Элементы.ПрикрепленныйФайл.Заголовок = Объект.ИмяФайла;	
		Элементы.СохранитьФайл.Видимость = Истина;
		Элементы.УдалитьФайл.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПрикрепленныйФайлНажатие(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ИмяФайла) Тогда
		
		НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("ПолучениеКаталогаВременныхЗавершение",ЭтаФорма));
		
	Иначе
		ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбора.ПолноеИмяФайла = "";
		ДиалогВыбора.Фильтр = НСтр("ru = 'Файл PDF'; en = 'PDF file'") + "(*.pdf)|*.pdf";
		ДиалогВыбора.МножественныйВыбор = Ложь;
		ДиалогВыбора.Заголовок = "Выберите файл";
		
		НачатьПомещениеФайлов(Новый ОписаниеОповещения("ОбработкаОповещенияВыбораФайла", ЭтаФорма),,ДиалогВыбора,Истина,УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеКаталогаВременныхЗавершение(ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт 
	
	//Адрес = ПолучитьАдресПрикрепленногоФайла();
	Адрес = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ПрикрепленныйФайл");
	ПутьКФайлу = ИмяКаталогаВременныхФайлов + Объект.ИмяФайла;
	
	МассивФайлов = Новый Массив;
    МассивФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(ПутьКФайлу, Адрес));
	
	НачатьПолучениеФайлов(Новый ОписаниеОповещения("ПолучениеФайловЗавершение", ЭтаФорма, Истина), МассивФайлов,, Ложь); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеФайловЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт 
	
	Если ПолученныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ДополнительныеПараметры Тогда
		НачатьЗапускПриложения(Новый ОписаниеОповещения("ЗапускПриложенияЗавершение", ЭтаФорма),ПолученныеФайлы[0].Имя,,Ложь); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускПриложенияЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт 
	// ничего не делаем	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияВыбораФайла(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ПомещенныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйФайл = ПомещенныеФайлы[0];
	Объект.ИмяФайла = РаботаСФайлами.РазборПутиКФайлу(ВыбранныйФайл.Имя);
    Адрес = ВыбранныйФайл.Хранение;
	
	ОбработкаОповещенияВыбораФайлаНаСервере(Адрес);
	
	УстановитьВидимость();

КонецПроцедуры	

&НаСервере
Процедура ОбработкаОповещенияВыбораФайлаНаСервере(Адрес)
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Объект");
	ДанныеОбъекта.ПрикрепленныйФайл = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Адрес));
	ДанныеОбъекта.Записать();
	ЗначениеВРеквизитФормы(ДанныеОбъекта, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПрикрепленныйФайлНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.ИмяФайла) Тогда
		Возврат;	
	КонецЕсли;
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбора.ПолноеИмяФайла = Объект.ИмяФайла;
	ДиалогВыбора.Фильтр = НСтр("ru = 'Файл PDF'; en = 'PDF file'") + "(*.pdf)|*.pdf";
	ДиалогВыбора.МножественныйВыбор = Ложь;
	ДиалогВыбора.Заголовок = "Выберите каталог для сохранения файла";
	
	Адрес = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ПрикрепленныйФайл");
	ПутьКФайлу = Объект.ИмяФайла;
	
	МассивФайлов = Новый Массив;
    МассивФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(ПутьКФайлу, Адрес));
	
	НачатьПолучениеФайлов(Новый ОписаниеОповещения("ПолучениеФайловЗавершение", ЭтаФорма, Ложь),МассивФайлов,ДиалогВыбора,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	 УстановитьВидимость();

КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлНажатие(Элемент)
	
    ПоказатьВопрос(Новый ОписаниеОповещения("УдалитьФайлЗавершение", ЭтотОбъект),
                        "Файл будет безвозвратно удален и элемент записан.
                        |Продолжить?", РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Да,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт 
    
    Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
	
	УдалитьПрикрепленныйФайлНаСервере();
	
	УстановитьВидимость();
   
КонецПроцедуры 

&НаСервере
Процедура УдалитьПрикрепленныйФайлНаСервере()
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Объект");
	ДанныеОбъекта.ПрикрепленныйФайл = Неопределено;
	ДанныеОбъекта.ИмяФайла = "";
	ДанныеОбъекта.Записать();
	ЗначениеВРеквизитФормы(ДанныеОбъекта, "Объект");
	
КонецПроцедуры
